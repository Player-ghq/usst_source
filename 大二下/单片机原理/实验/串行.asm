/**********************慧净电子51实验板例程************************
*  平台：STC89C52
*  名称：中断服务程序例程5-1
*  晶振:11.0592MHZ	 
******************************************************************/
		ORG    0000H
RESET:	LJMP   MAIN   ;上电，转向主程序
		ORG    0023H   ;串口中断入口地址
		JMP    SERIAL_SERVICE   ;转向中断服务程序

;**************************************
;main
;**************************************
      	ORG    	0030H 	;主程序

MAIN: 	MOV 	SP,#40H 	 ;设置堆栈指针
		MOV    	TMOD,#20H    ;定时器1设为模式2
    	  	MOV    	TL1,#0FDH    ;定时器初值  f=11059200,baut=9600b/s
   	   	MOV    	TH1,#0FDH    ;8位重装值
    	  	SETB   	TR1          ;启动定时器1
    	  	MOV    	SCON ,#50H   ;设置为方式1， 
			          		 ;REN=1
		MOV    	R0,#00H      ;计数清零
      		SETB   	ES
      		SETB   	EA
LOOP:	JB     	P3.5,LOOP    ;查询按键K2是否按下
		ACALL  	DELAY		 ;延时消抖
		JB     	P3.5,LOOP	 ;判断按键K2是否一直按下
		JNB    	P3.5,$		 ;等待按键放开 
		INC    	R0           ;计数+1
		ACALL  	SOUT
		AJMP	LOOP
;**************************************
;中断服务程序
;**************************************
		ORG    	0100H  
SERIAL_SERVICE:
     	JNB    	RI ,SEND   ;TI=1,为发送中断
		CLR		RI
      	ACALL  	SIN        ;RI=1,为接收中断
      	SJMP   	NEXT 	  ;转至统一的出口
SEND: 
     	CLR		TI	      ;调用发送子程序
NEXT: 
	    RETI                     ;中断返回
;**************************************
;发送子函数
;**************************************
SOUT: 	MOV		A ,R0       ;取按键次数A
        MOV  	SBUF , A    ;发送ASCII码
        RET              	;返回
;**************************************
;接收子函数
;**************************************
SIN:   	MOV 	A ,SBUF   ;读出接收缓冲区内容
		CPL		A
		MOV	    P1,A	  ;点亮LED
       	RET               ;返回
;**************************************
;延时子函数10ms左右
;**************************************
DELAY:	MOV     R2,#020
D1:		MOV		R3,#0FFH
D2:		DJNZ	R3,D2
		DJNZ	R2,D1
		RET
;**************************************
	  	END